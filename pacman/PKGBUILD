# Maintainer: Connor <chkycc@gmail.com>

pkgname=python-srun_login
_pkgname=srun_login
pkgver=0.1.0
pkgrel=1
pkgdesc='a tool to login srun network'
arch=('any')
license=('GPL-1.0-or-later')
depends=('python-requests' 'python-pycryptodome')
makedepends=('python-setuptools-scm'
             'python-wheel'
             'python-build'
             'python-installer')
source=("${_pkgname}-${pkgver}.tar.gz"
        "srun_login.service")
sha256sums=('SKIP'
            'b4ee85cf21691782f8f18255cc5fcfe921888c2acd860f94efbb023e1925a5b4')

build() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python -m build --wheel --no-isolation
}

package() {
  cd "$srcdir/${_pkgname}-${pkgver}"
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm644 -t "$pkgdir/usr/lib/systemd/system" "$srcdir/srun_login.service"
}
# vim: set ft=sh ts=2 sw=2 et:
